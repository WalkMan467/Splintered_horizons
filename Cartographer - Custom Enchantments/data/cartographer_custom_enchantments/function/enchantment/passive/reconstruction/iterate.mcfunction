data modify storage cartographer_custom_enchantments:reconstruction item_check set value {}
data modify storage cartographer_custom_enchantments:reconstruction item_check set from storage cartographer_custom_enchantments:reconstruction items[0]

data modify storage cartographer_custom_enchantments:reconstruction macro set value {}
data modify storage cartographer_custom_enchantments:reconstruction macro.level set from storage cartographer_custom_enchantments:reconstruction item_check.components.minecraft:enchantments.cartographer_custom_enchantments:passive/reconstruction
execute store result storage cartographer_custom_enchantments:reconstruction macro.slot int 1 run data get storage cartographer_custom_enchantments:reconstruction item_check.Slot

execute if data storage cartographer_custom_enchantments:reconstruction item_check.components.minecraft:enchantments.cartographer_custom_enchantments:passive/reconstruction run function cartographer_custom_enchantments:enchantment/passive/reconstruction/attempt_repair with storage cartographer_custom_enchantments:reconstruction macro

data remove storage cartographer_custom_enchantments:reconstruction items[0]
execute if data storage cartographer_custom_enchantments:reconstruction items[0] run function cartographer_custom_enchantments:enchantment/passive/reconstruction/iterate